
func ConnectIslands(
    rand &XS32Rand,
    map &PatternMap<bool>,
    // 1 for 1-width paths, 2 for 3-width paths, 3 for 5-width paths, etc.
    connectingPathRadius int) {
  nextIslandIndex = 0;
  locToIslandIndex = HashMap<Location, int>(LocationHasher(), LocationEquator());
  islandIndexToArbitraryMemberLoc = HashMap<int, Location>(IntHasher(), IntEquator());

  println("Finding spark locs!");
  foreach [sparkLoc, b] in &map.tiles {
    if not locToIslandIndex.ContainsKey(sparkLoc) {
      println("Found spark loc: {sparkLoc}");
      islandIndex = set nextIslandIndex = nextIslandIndex + 1;
      islandIndexToArbitraryMemberLoc.add(islandIndex, sparkLoc);
      FloodRealm(&map, &locToIslandIndex, islandIndex, sparkLoc);
    }
  }

  islandIndexToKingdomId = []int(nextIslandIndex, {_});
  while ConnectDistinctKingdoms(
      map,
      &locToIslandIndex,
      &islandIndexToArbitraryMemberLoc,
      &islandIndexToKingdomId,
      connectingPathRadius) {
  }
  
}

func FloodRealm(
    map &PatternMap<bool>,
    locToIslandIndex &HashMap<Location, int, LocationHasher, LocationEquator>,
    islandIndex int,
    thisLoc Location) {
  if locToIslandIndex.ContainsKey(thisLoc) {
    ret void();
  }
  locToIslandIndex.add(thisLoc, islandIndex);
  neighbors = map.GetAdjacentExistingLocations(thisLoc, false);
  foreach neighborLoc in &neighbors {
    FloodRealm(map, locToIslandIndex, islandIndex, neighborLoc);
  }
}

func ConnectDistinctKingdoms(
    map &PatternMap<bool>,
    locToIslandIndex &HashMap<Location, int, LocationHasher, LocationEquator>,
    islandIndexToArbitraryMemberLoc &HashMap<int, Location, IntHasher, IntEquator>,
    islandIndexToKingdomId &[]int,
    connectingPathRadius int)
// True if we united any distinct kingdoms
bool {
  println("Looking for kingdoms to connect!");
  tentativeSourceIslandIndex = 0;
  tentativeSourceKingdomId = islandIndexToKingdomId[tentativeSourceIslandIndex];
  foreach tentativeTargetIslandIndex in range(0, islandIndexToKingdomId.len()) {
    tentativeTargetKingdomId = islandIndexToKingdomId[tentativeTargetIslandIndex];
    println("Considering other island {tentativeTargetIslandIndex} kingdom {tentativeTargetKingdomId}");
    if tentativeTargetKingdomId != tentativeSourceKingdomId {
      tentativeSourceLoc = islandIndexToArbitraryMemberLoc.get(tentativeSourceIslandIndex).get();
      tentativeTargetLoc = islandIndexToArbitraryMemberLoc.get(tentativeTargetIslandIndex).get();

      println("Getting path from {tentativeSourceLoc} to {tentativeTargetLoc}!");
      path =
          Drive(
              &map, &locToIslandIndex, islandIndexToKingdomId, tentativeSourceLoc, tentativeTargetLoc);
      pathLocsSet =
          HashSet<Location, LocationHasher, LocationEquator>(
              LocationHasher(), LocationEquator());
      foreach loc in &path {
        pathLocsSet.add(loc);
      }
      
      // Expand (connectingPathRadius - 1) times
      foreach i in range(0, connectingPathRadius - 1) {
        println("Expanding! length before: " + pathLocsSet.len());
        foreach loc in map.pattern.GetAdjacentLocations(&pathLocsSet, false, false) {
          if not locToIslandIndex.ContainsKey(loc) {
            pathLocsSet.add(loc);
          }
        }
        println("Expanding! length after: " + pathLocsSet.len());
      }

      pathAndNeighborLocs =
          map.pattern.GetAdjacentLocations(
              &pathLocsSet.HashSet(LocationHasher(), LocationEquator()),
              true, false);

      pathAndNeighborKingdomIds =
          HashSet<int, IntHasher, IntEquator>(
              IntHasher(), IntEquator());

      println("Uniting kingdoms:");
      foreach loc in &pathAndNeighborLocs {
        if locToIslandIndex.ContainsKey(loc) {
          kingdomId = islandIndexToKingdomId[locToIslandIndex.get(loc).get()];
          if not pathAndNeighborKingdomIds.contains(kingdomId) {
            println("- Kingdom {kingdomId}!");
            pathAndNeighborKingdomIds.add(kingdomId);
          }
        }
      }

      vassert(not pathAndNeighborKingdomIds.isEmpty(), "Empty pathAndNeighborKingdomIds");

      // Now we need to unite all these kingdoms the path touches.
      victoriousKingdomId = pathAndNeighborKingdomIds.GetRandomElement(0).get();
      println("...into victorious kingdom {victoriousKingdomId}!");

      arbitraryReassigningIslandIndex = -1;
      foreach [islandIndex, hayKingdomId] in islandIndexToKingdomId.entries() {
        if pathAndNeighborKingdomIds.contains(hayKingdomId) and hayKingdomId != victoriousKingdomId {
          println("Reassigning island index {islandIndex} from kingdom {hayKingdomId} to {victoriousKingdomId}");
          set islandIndexToKingdomId[islandIndex] = victoriousKingdomId;
          set arbitraryReassigningIslandIndex = islandIndex;
        }
      }
      vassert(arbitraryReassigningIslandIndex != -1);
      println("Arbitrary member island index {arbitraryReassigningIslandIndex}");

      foreach loc in &pathLocsSet {
        println("Creating path loc {loc} as part of island index {arbitraryReassigningIslandIndex}!");
        map.tiles.add(loc, true);
        locToIslandIndex.add(loc, arbitraryReassigningIslandIndex);
      }

      ret true;
    }
  }

  ret false;
}


func Drive(
    map &PatternMap<bool>,
    locToIslandIndex &HashMap<Location, int, LocationHasher, LocationEquator>,
    islandIndexToKingdomId &[]int,
    sourceLoc Location,
    targetLoc Location)
// Returns a path that grazes &at least* two different kingdoms.
[]Location {

  sourceKingdomId = islandIndexToKingdomId[locToIslandIndex.get(sourceLoc).get("bork a")];
  targetKingdomId = islandIndexToKingdomId[locToIslandIndex.get(targetLoc).get("bork b")];
  vassert(sourceKingdomId != targetKingdomId);

  [originalPathWithoutSource] =
    Go(
        map.pattern,
        sourceLoc,
        targetLoc,
        false,
        (from, to, distanceSoFar) => {
          map.pattern.IsAdjacent(from, to, false)
        });

  // Should be impossible because we're just navigating on the pattern, not the terrain.
  vassert(originalPathWithoutSource.len() > 0, "No pattern path?");

  vassert(originalPathWithoutSource[originalPathWithoutSource.len() - 1] == targetLoc);
  println("Have path! len {originalPathWithoutSource.len()}");

  vassert(originalPathWithoutSource[0] != sourceLoc);
  // We don't get the source location included, so lets add that.
  originalPath = List<Location>();
  originalPath.add(sourceLoc);
  foreach x in originalPathWithoutSource {
    originalPath.add(x);
  }

  println("Added original loc!");

  // The index in the originalPath of the latest step that's in a kingdom that's not the target kingdom
  lastForeignKingdomStepIndex = 0;
  maybeFirstTargetKingdomStepIndex Opt<int> = None<int>();
  foreach [index, stepLoc] in originalPath.entries() {
    if not locToIslandIndex.ContainsKey(stepLoc) {
      // Tile doesnt exist, keep looking
    } else if islandIndexToKingdomId[locToIslandIndex.get(stepLoc).get("bork c")] == targetKingdomId {
      if maybeFirstTargetKingdomStepIndex.isEmpty() {
        set maybeFirstTargetKingdomStepIndex = Some(index);
      }
    } else {
      set lastForeignKingdomStepIndex = index;
    }
  }
  firstTargetKingdomStepIndex = maybeFirstTargetKingdomStepIndex.get("bork d");

  println("Found subpath! {lastForeignKingdomStepIndex} to {firstTargetKingdomStepIndex}");

  // originalPath might have cut through foreign island locs, and it might have also reached
  // a target kingdom loc before the target loc.
  // Just make a path between from last foreign kingdom loc and the first target kingdom loc.

  // Should exist
  vassert(map.tiles.ContainsKey(originalPath.get(lastForeignKingdomStepIndex)));
  // Well actually, lets start one past it, because this should be empty space.
  start_index = lastForeignKingdomStepIndex + 1;
  // Should be empty space
  vassert(not map.tiles.ContainsKey(originalPath.get(start_index)));

  // Should exist
  vassert(map.tiles.ContainsKey(originalPath.get(lastForeignKingdomStepIndex)));
  // And lets start one before the end, should also be empty space.
  last_index = firstTargetKingdomStepIndex - 1;
  // Should be empty space
  vassert(not map.tiles.ContainsKey(originalPath.get(last_index)));

  path = List<Location>();

  foreach index in range(start_index, last_index + 1) {
    // Should be empty space
    vassert(not map.tiles.ContainsKey(originalPath.get(index)));

    path.add(originalPath.get(index));
  }

  ret (path).toArray();
}
